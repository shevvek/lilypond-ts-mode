(module-define! (resolve-module '(geiser doc))
                'lily-mod
                (current-module))
(set-current-module (resolve-module '(geiser doc)))
(use-modules (srfi srfi-11)
             (ice-9 and-let-star))

(let ((old-arguments arguments))
  (set! arguments
        (lambda (proc)
          (or (old-arguments proc)
              (ly-primitive-args proc)))))

(define (ly-primitive-args proc)
  (and-let* (((program? proc))
             (doc (object-documentation proc))
             (arities (program-arguments-alist proc))
             (arg-names (map (lambda (m)
                               (string->symbol
                                (match:substring m 1)))
                             (list-matches "SCM ([^ ,()]+)"
                                           doc)))
             (req-count (length (assq-ref arities 'required)))
             (opt-count (length (assq-ref arities 'optional)))
             (kw-count (length (assq-ref arities 'keyword)))
             (rest-count (if (assq-ref arities 'rest) 1 0))
             ((= (length arg-names)
                 (+ req-count opt-count kw-count rest-count))))
    (let*-values
        (((req-args rest) (split-at arg-names req-count))
         ((opt-args rest) (split-at rest opt-count))
         ((kw-args rest) (split-at rest kw-count))
         ((rest) (and (pair? rest) (car rest)))
         )
      `(((required . ,req-args)
         (optional . ,opt-args)
         (keyword . ,kw-args)
         (rest . ,rest))))))

(define-public (all-keywords-of-type pred)
  (filter (lambda (binding)
            (pred (module-ref (current-module) binding)))
          (apropos-internal "")))

(define-public (music-completions prefix)
  (let ((prefix (string-append "^" (regexp-quote prefix))))
    (sort! (filter-map (lambda (s)
                         (let ((c (ly:parser-lookup s)))
                           (and (or (ly:music? c)
                                    (ly:music-function? c)
                                    (ly:context-mod? c)
                                    (ly:translator? c)
                                    (markup? c)
                                    (markup-function? c)
                                    (markup-list-function? c)
                                    (and (markup-list? c)
                                         (pair? c)))
                                (symbol->string s))))
                       (apropos-internal prefix))
           string<?)))

(set-current-module lily-mod)
